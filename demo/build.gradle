apply plugin: 'com.android.application'

static def gitLatestTag() {
    try {
        def cmd = 'git describe --abbrev=0 --tags'
        def result = cmd.execute().text.trim()
        return result
    } catch (Exception e) {
        return '1.0.0'
    }
}

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "me.yokeyword.sample"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName gitLatestTag()
    }

    signingConfigs {
        releaseConfig {
            storeFile file("./fragmentation.jks")
            storePassword System.getenv("KEYSTORE_PASS")
            keyAlias System.getenv("ALIAS_NAME")
            keyPassword System.getenv("ALIAS_PASS")
        }
    }

    buildTypes {
        release {
            minifyEnabled false

            signingConfig signingConfigs.releaseConfig

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }


    android.applicationVariants.all { variant ->
        variant.outputs.all {
            if (variant.buildType.name == 'release') {
                outputFileName = "fragmentation.apk"
            }
        }
    }
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'
    testApi 'junit:junit:4.12'
    api 'androidx.appcompat:appcompat:1.2.0'
    api 'com.google.android.material:material:1.2.1'
    api 'androidx.cardview:cardview:1.0.0'
    api 'androidx.recyclerview:recyclerview:1.1.0'
    api project(':fragmentation')
    api project(':fragmentation_swipeback')
    // EventBus
    api project(':eventbus_activity_scope')
    api 'org.greenrobot:eventbus:3.2.0'
    //    apt 'org.greenrobot:eventbus-annotation-processor:3.0.1'

}

